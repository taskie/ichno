pkgname=ichno-git
pkgver=0.1.1.r43.01dc348
pkgrel=1
pkgdesc="Calculate hashes of Git tree and blob, outside working directory."
arch=('x86_64')
url="https://github.com/taskie/ichno"
license=('Apache-2.0')
provides=("ichno")
makedepends=('cargo' 'git')
optdepends=()
depends=()
conflicts=('ichno')
source=("$pkgname::git+https://github.com/taskie/ichno")
sha1sums=('SKIP')

build() {
  cd "$pkgname"
  cd ichno_cli
  if command -v rustup > /dev/null 2>&1; then
    RUSTFLAGS="-C target-cpu=native" rustup run nightly \
      cargo build --release
  elif rustc --version | grep -q nightly; then
    RUSTFLAGS="-C target-cpu=native" \
      cargo build --release
  else
    cargo build --release
  fi
}

pkgver() {
  cd "$pkgname"
  local tag=$(git tag --sort=-v:refname | grep '^v[0-9]' | head -1)
  local commits_since=$(git rev-list "${tag}..HEAD" --count)
  echo "${tag##v}.r${commits_since}.$(git log --pretty=format:'%h' -n 1)"
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/ichno" "$pkgdir/usr/bin/ichno"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
