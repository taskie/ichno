pkgname=treblo-git
pkgver=0.1.0
pkgrel=1
pkgdesc="Calculate hashes of Git tree and blob, outside working directory."
arch=('x86_64')
url="https://github.com/taskie/treblo"
license=('Apache-2.0')
provides=("treblo")
makedepends=('cargo' 'git')
optdepends=()
depends=()
conflicts=('treblo')
source=("$pkgname::git+https://github.com/taskie/treblo")
sha1sums=('SKIP')

build() {
  cd "$pkgname"
  if command -v rustup > /dev/null 2>&1; then
    RUSTFLAGS="-C target-cpu=native" rustup run nightly \
      cargo build --release
  elif rustc --version | grep -q nightly; then
    RUSTFLAGS="-C target-cpu=native" \
      cargo build --release
  else
    cargo build --release
  fi
}

pkgver() {
  cd "$pkgname"
  local tag=$(git tag --sort=-v:refname | grep '^v[0-9]' | head -1)
  local commits_since=$(git rev-list "${tag}..HEAD" --count)
  echo "${tag##v}.r${commits_since}.$(git log --pretty=format:'%h' -n 1)"
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/treblo" "$pkgdir/usr/bin/treblo"
  install -Dm644 "README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
